grammar org.agileware.natural.words.Words hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate words "http://www.agileware.org/natural/words/Words"

// Text Structure
////

WordsModel:
	sections+=Section*
;

// TODO: Section should have subtype NamedSection
Section: Paragraph;

Paragraph: {Paragraph}
	text=Text
;

Text:
	elements+=Literal+
;

// Word Literals
////

Literal: StyledText 
	| {Literal} Word
	| {Literal} PUNCTUATION 
	| {Literal} NL
;

StyledText: SymbolicWord
	| {StyledText} Number 
	| {StyledText} String 
;

// TODO: provide additional symbolic keyword matching options
SymbolicWord: 
	L_ANGLE_BRACKET
	value=Word
	R_ANGLE_BRACKET
;

Word: SIMPLE_WORD;

Number: NUM;

// TODO: should provide access to string contents and surrounding quote type
String: STR;


// Common Terminals
////

terminal fragment SIMPLE_WORD_FRAGMENT: ALPHA_NUM (ALPHA_NUM | '-' | '_')*;
terminal fragment ALPHA_NUM: (LETTER | DIGIT);
terminal fragment LETTER: ('a'..'z'|'A'..'Z');
terminal fragment DIGIT: ('0'..'9');
terminal fragment NEW_LINE: ('\r'? '\n');
terminal fragment SPACE: (' ' | '\t');

terminal NUM returns ecore::EDouble:
	'-'? (DIGIT)+ ('.' (DIGIT)+)?;
	
terminal STR returns ecore::EString:
		'"' ( '\\' . | !('\\'|'"') )* '"' |
		"'" ( '\\' . | !('\\'|"'") )* "'"
;

terminal SIMPLE_WORD: SIMPLE_WORD_FRAGMENT;

PUNCTUATION:  '!'
    | '-'
    | '--'
    | '$'
    | '%'
    | '&'
    | '\''
    | '('
    | ')'
    | '+'
    | ','
    | '.'
    | '/'
    | '\\'
    | '='
    | '?'
    | '['
    | ']'
    | '^'
    | '{'
    | '}'
    | '~'
    | '...'
 ;
 
terminal L_ANGLE_BRACKET: '<';
terminal R_ANGLE_BRACKET: '>';

terminal NL: NEW_LINE+ SPACE*;
terminal WS: SPACE+;
